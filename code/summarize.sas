/* ********************************************************************************* */
/*                                                                                   */
/* Macro   : summarize                                                               */
/* Summary : Auxiliary macro for plots that creates and merges summary variables.    */
/* Input   : The main output data.                                                   */
/* Output  : Datasets that contain rank and summary variables on the main output.    */
/*                                                                                   */
/* Author  : Georgios Magkotsios                                                     */
/* Created : July 2012                                                               */
/*                                                                                   */
/* ********************************************************************************* */

%macro summarize;

    /* rank the data by total sizes */
    proc rank data=&DataNBBO (where=(Symbol in &SymbolList and
        (BestBidSize   >= &BestSizeThreshold or
         BestOfferSize >= &BestSizeThreshold)))
        out=ranked_NBBO groups=&RankGroups;
        by Date Symbol;
        var TotalSize TotalLogSize MinBestSize;
        ranks TotalSizeRank TotalLogSizeRank MinBestSizeRank;
    run;
    proc rank data=&DataMain (where=(Symbol in &SymbolList and
        (BestBidSize   >= &BestSizeThreshold or
         BestOfferSize >= &BestSizeThreshold)))
        out=ranked_trades groups=&RankGroups;
        by Date Symbol;
        var TotalSize TotalLogSize MinBestSize;
        ranks TotalSizeRank TotalLogSizeRank MinBestSizeRank;
    run;
    

    /* get the summary statistics of the NBBO data */
    proc means data=ranked_NBBO
        /*(where=(&RankVariable between &RankValueMin and &RankValueMax))*/ noprint;
        class ImplicitPriceBin;
        var ImplicitPriceMod;
        output out = nbbo_means mean(ImplicitPriceMod) = MeanImplicitPriceModNBBO;
    run;

    /* get the summary statistics of the trade data */
    proc means data=ranked_trades
        /*(where=(&RankVariable between &RankValueMin and &RankValueMax))*/ noprint;
        class ImplicitPriceBin;
        var ImplicitPriceMod TradeIndicatorQ;
        output out = trade_means
            mean(ImplicitPriceMod TradeIndicatorQ) =
                 MeanImplicitPriceModTrades MeanTradeIndicatorQ;
    run;

    /* merge the two data sets generated by proc means */
    data merge_means (drop=_TYPE_);
        merge nbbo_means (where=(_TYPE_ = 1) rename=(_FREQ_=FreqNBBO))
              trade_means (where=(_TYPE_ = 1) rename=(_FREQ_=FreqTrades));
        by ImplicitPriceBin;
        label
            FreqNBBO = 'Frequency distribution of tick fractional part (NBBO)'
            FreqTrades = 'Frequency distribution of tick fractional part (trades)'
            MeanImplicitPriceModNBBO = 'Mean tick fractional part in bin (NBBO)'
            MeanImplicitPriceModTrades = 'Mean tick fractional part in bin (trades)'
            MeanTradeIndicatorQ = 'Mean proximity of trade price to NBBO in bin';
    run;

    %if (&DebugMode) %then %do;
        %*debug(ranked_NBBO);
        %*debug(ranked_trades);
        %debug(nbbo_means);
        %debug(trade_means);
        %debug(merge_means);
    %end;

%mend summarize;
